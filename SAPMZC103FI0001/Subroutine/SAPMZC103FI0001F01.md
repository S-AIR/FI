``` abap
*&---------------------------------------------------------------------*
*& Include          SAPMZC103FI0001F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form display_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen .

  LOOP AT SCREEN.

    IF screen-group1 EQ 'COL'.
      screen-intensified = 1.
      MODIFY SCREEN.
    ENDIF.

  ENDLOOP.

  IF go_container IS NOT BOUND.

    PERFORM set_description.
    PERFORM get_data.
    PERFORM set_screen.

    CLEAR : gt_fcat, gs_fcat.

    PERFORM set_field_catalog USING : 'X' 'BUKRS'      'ZC103FIT0003' 'C' ' ',
                                      'X' 'SAKNR'      'ZC103FIT0003' 'C' ' ',
                                      ' ' 'TXT50'      'ZC103FIT0003' ' ' 'X',
                                      ' ' 'XINTB'      'ZC103FIT0003' 'C' ' ',
                                      ' ' 'XLOEV'      'ZC103FIT0003' 'C' ' ',
                                      ' ' 'WAERS'      'ZC103FIT0003' 'C' ' '.

    PERFORM set_layout.
    PERFORM exclude_toolbar.
    PERFORM create_object.

    SET HANDLER : lcl_event_handler=>modify_value FOR go_alv_grid,
                  lcl_event_handler=>edit_toolbar FOR go_alv_grid,
                  lcl_event_handler=>user_command FOR go_alv_grid.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_variant
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_layout
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_body
        it_fieldcatalog      = gt_fcat.

    PERFORM register_event.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat-key       = pv_key.
  gs_fcat-fieldname = pv_field.
  gs_fcat-ref_table = pv_table.
  gs_fcat-just      = pv_just.
  gs_fcat-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'BUKRS'.
      gs_fcat-coltext = '회사코드'.
    WHEN 'SAKNR'.
      gs_fcat-coltext = 'G/L계정'.
    WHEN 'TXT50'.
      gs_fcat-coltext = '계정명'.
*      gs_fcat-emphasize = pv_emph.
    WHEN 'XINTB'.
      gs_fcat-coltext = '자동전표입력'.
      gs_fcat-checkbox = 'X'.
    WHEN 'XLOEV'.
      gs_fcat-coltext = '삭제여부'.
      gs_fcat-checkbox = 'X'.
    WHEN 'WAERS'.
      gs_fcat-coltext = '통화'.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

  gs_layout-zebra      = abap_true.
  gs_layout-cwidth_opt = 'A'.
  gs_layout-sel_mode   = 'A'.
  gs_layout-stylefname = 'CELL_TAB'.
  gs_layout-grid_title = 'G/L계정 항목'.

  gs_variant-report = sy-repid.
  gs_variant-handle = 'ALV1'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  " 1. 메인 커스텀 컨테이너 생성
  CREATE OBJECT go_base_container
    EXPORTING
      container_name = 'MAIN_CONT'. " Screen Painter에서 만든 커스텀컨테이너 이름

  " 2. 스플리터 컨테이너 생성 (2개의 셀, 좌우로 나눔)
  CREATE OBJECT go_split_cont
    EXPORTING
      parent  = go_base_container
      rows    = 1
      columns = 2.

  " 3. 왼쪽 셀에 ALV용 컨테이너 생성
  CALL METHOD go_split_cont->get_container
    EXPORTING
      row       = 1
      column    = 1
    RECEIVING
      container = go_container. " ALV 용 컨테이너

  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_container.

  " 4. 오른쪽 셀에 차트용 컨테이너 생성
  CALL METHOD go_split_cont->get_container
    EXPORTING
      row       = 1
      column    = 2
    RECEIVING
      container = go_chart_container. " 차트 용 컨테이너

  CREATE OBJECT go_chart
    EXPORTING
      parent = go_chart_container.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form exclude_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM exclude_toolbar .

  DATA : ls_ui_functions TYPE ui_func.

  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_undo.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_cut.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_delete_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_insert_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_append_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste_new_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_refresh.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_auf.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_average.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_print.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_graph.
  APPEND ls_ui_functions TO gt_ui_functions.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_event
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_event .

  CALL METHOD go_alv_grid->set_ready_for_input
    EXPORTING
      i_ready_for_input = 0.

  CALL METHOD go_alv_grid->register_edit_event
    EXPORTING
      i_event_id = cl_gui_alv_grid=>mc_evt_modified.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_modify_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_MODIFIED
*&      --> ET_GOOD_CELLS
*&---------------------------------------------------------------------*
FORM handle_modify_value  USING pv_modified
                                pt_good_cells TYPE lvc_t_modi.

*  DATA : ls_good_cells TYPE LINE OF lvc_t_modi.
  DATA : ls_good_cells TYPE lvc_s_modi.

  CHECK pv_modified IS NOT INITIAL. " ALV에서 실제로 Data변경이 일어났는지 확인

  LOOP AT pt_good_cells INTO ls_good_cells.

*-- Set costcenter name

*-- Set Calculation

*-- Set changed flag
    gs_body-modi_yn = abap_true.

    MODIFY gt_body FROM gs_body INDEX ls_good_cells-row_id
                                TRANSPORTING modi_yn.

  ENDLOOP.

*  PERFORM refresh_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_table
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_table .

  DATA : ls_stable  TYPE lvc_s_stbl.

*-- 현재 Cursor 위치를 Yuji : From Yongsan
  ls_stable-row = abap_true.
  ls_stable-col = abap_true.

*-- Refresh ALV
  CALL METHOD go_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_OBJECT
*&      --> E_INTERACTIVE
*&---------------------------------------------------------------------*
FORM handle_edit_toolbar  USING po_object TYPE REF TO cl_alv_event_toolbar_set
                                pv_interactive.

  DATA : lv_disabled.

  IF gv_mode EQ 'D'.
    lv_disabled = abap_true.
  ENDIF.

  CLEAR gs_button.
  gs_button-butn_type = '3'.
  APPEND gs_button TO po_object->mt_toolbar.

  CLEAR gs_button.
  gs_button-function  = 'TOGL'.
  gs_button-icon      = icon_toggle_display_change.
  gs_button-quickinfo = 'Display <-> Change'.
  APPEND gs_button TO po_object->mt_toolbar.

  CLEAR gs_button.
  gs_button-butn_type = '3'.
  APPEND gs_button TO po_object->mt_toolbar.

  CLEAR gs_button.
  gs_button-function  = 'IROW'.
  gs_button-icon      = icon_insert_row.
  gs_button-quickinfo = 'Insert row'.
  gs_button-disabled  = lv_disabled.
  APPEND gs_button TO po_object->mt_toolbar.

  CLEAR gs_button.
  gs_button-function  = 'DROW'.
  gs_button-icon      = icon_delete_row.
  gs_button-quickinfo = 'Delete row'.
  gs_button-disabled  = lv_disabled.
  APPEND gs_button TO po_object->mt_toolbar.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&---------------------------------------------------------------------*
FORM handle_user_command  USING pv_ucomm.

  CASE pv_ucomm.
    WHEN 'TOGL'. " Display <-> Change
      PERFORM toggle_change.
    WHEN 'IROW'. " Insert row
      PERFORM insert_row.
    WHEN 'DROW'. " Delete row
      PERFORM delete_row.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form toggle_change
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM toggle_change .

  CASE gv_mode.
    WHEN 'E'.
      gv_mode = 'D'.
      CALL METHOD go_alv_grid->set_ready_for_input
        EXPORTING
          i_ready_for_input = 0.
    WHEN 'D'.
      gv_mode = 'E'.
      CALL METHOD go_alv_grid->set_ready_for_input
        EXPORTING
          i_ready_for_input = 1.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form insert_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM insert_row .

  DATA : ls_style TYPE lvc_s_styl.

  CLEAR gs_body.
  ls_style-style = cl_gui_alv_grid=>mc_style_enabled.
  INSERT ls_style INTO TABLE gs_body-cell_tab.

  APPEND gs_body TO gt_body.

*-- Refresh ALV
  PERFORM refresh_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form delete_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM delete_row .

  DATA : lt_roid TYPE lvc_t_roid,
         ls_roid TYPE lvc_s_roid.

*-- 사용자가 선택한 행 정보를 받아온다.
  CALL METHOD go_alv_grid->get_selected_rows
    IMPORTING
      et_row_no = lt_roid.

*-- 삭제할 행을 선택하지 않고 Delete row 버튼 누르면 오류
  IF lt_roid IS INITIAL.
    MESSAGE s000 WITH TEXT-e01 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

*-- Index를 역순으로 정렬하여 밑부분 부터 삭제
  SORT lt_roid BY row_id DESCENDING.

*-- Delete row
  LOOP AT lt_roid INTO ls_roid.

*-- 삭제대상 데이터 Backup
    READ TABLE gt_body INTO gs_body INDEX ls_roid-row_id.
    MOVE-CORRESPONDING gs_body TO gs_delt.
    APPEND gs_delt TO gt_delt.
    CLEAR gs_delt.

*-- ITAB에서 삭제
    DELETE gt_body INDEX ls_roid-row_id.

  ENDLOOP.

  PERFORM refresh_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_data .

  DATA : lt_save           TYPE TABLE OF zc103fit0003,
         ls_save           TYPE zc103fit0003,
         lv_tabix          TYPE sy-tabix,
         lv_flag,
         lv_answer,
         lv_saknr_fixed(6).

*-- ALV상에서 변경된 데이터가 있는지 체크후 Internal table에 반영.
  CALL METHOD go_alv_grid->check_changed_data.

*-- 변경사항이 있는지 확인
  CLEAR gs_body.
  READ TABLE gt_body INTO gs_body WITH KEY modi_yn = abap_true.
  IF ( sy-subrc NE 0 ) AND
     ( gt_delt IS INITIAL ).
    MESSAGE s001 WITH '변경되거나 삭제된 데이터가 없습니다.' DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

*-- 데이터를 저장하기전 사용자에게 확인(CONFIRM)을 받은후 처리한다.
  PERFORM confirm CHANGING lv_answer.
  CHECK lv_answer EQ '1'.

*-- Set Time stamp 및 유효성 체크 포함
  LOOP AT gt_body INTO gs_body WHERE modi_yn EQ abap_true.

    lv_tabix  = sy-tabix.

    lv_saknr_fixed = gs_body-saknr.

    " 전표번호 범위 체크
    IF lv_saknr_fixed >= '600000' OR lv_saknr_fixed < '100000'.
      MESSAGE '[저장 실패] 전표번호의 범위가 올바르지 않습니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 전표번호 자리값 기준 그룹코드 설정
    CASE gs_body-saknr(1).
      WHEN '1'.
        gs_body-saknr_gr_cd = '00'.
        gs_body-konts = 'D'.
      WHEN '2'.
        gs_body-saknr_gr_cd = '01'.
        gs_body-konts = 'D'.
      WHEN '3'.
        gs_body-saknr_gr_cd = '02'.
        gs_body-konts = 'D'.
      WHEN '4'.
        gs_body-saknr_gr_cd = '03'.
        gs_body-konts = 'P'.
      WHEN '5'.
        gs_body-saknr_gr_cd = '04'.
        gs_body-konts = 'P'.
      WHEN OTHERS.
        MESSAGE '[저장 실패] 전표번호의 범위가 올바르지 않습니다.' TYPE 'S' DISPLAY LIKE 'E'.
        RETURN.
    ENDCASE.

    " 자동전표여부 검증
    IF gs_body-xintb <> ' ' AND gs_body-xintb <> 'X'.
      MESSAGE '[저장 실패] 자동전표여부가 올바르지 않습니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 삭제여부 검증
    IF gs_body-xloev <> ' ' AND gs_body-xloev <> 'X'.
      MESSAGE '[저장 실패] 삭제여부가 올바르지 않습니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 통화코드 검증
    IF gs_body-waers <> 'KRW' AND gs_body-waers <> 'USD'.
      MESSAGE '[저장 실패] 통화코드가 올바르지 않습니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 회사코드 검증
    IF gs_body-bukrs <> '0001'.
      MESSAGE '자사 회사코드만 입력 가능합니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 중복 PK 체크 (동일 BUKRS + SAKNR, 다른 인덱스)
    READ TABLE gt_body WITH KEY bukrs = gs_body-bukrs
                                 saknr = gs_body-saknr
                      TRANSPORTING NO FIELDS.
    IF sy-subrc = 0 AND sy-tabix <> lv_tabix.
      MESSAGE '[저장 실패] 중복된 PK(BUKRS + SAKNR)값이 존재합니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    " 생성/변경자 처리
    IF gs_body-erdat IS INITIAL.
      gs_body-erdat = sy-datum.
      gs_body-ernam = sy-uname.
      gs_body-erzet = sy-uzeit.
    ELSE.
      gs_body-aedat = sy-datum.
      gs_body-aenam = sy-uname.
      gs_body-aezet = sy-uzeit.
    ENDIF.

    CLEAR gs_body-modi_yn.

    MODIFY gt_body FROM gs_body INDEX lv_tabix
                                TRANSPORTING saknr_gr_cd erdat ernam erzet
                                             aedat aenam aezet
                                             modi_yn.

  ENDLOOP.

*-- Move data for save
  MOVE-CORRESPONDING gt_body TO lt_save.

*-- 삭제 대상 처리
  IF gt_delt IS NOT INITIAL.
    DELETE zc103fit0003 FROM TABLE gt_delt.
  ENDIF.

  CLEAR : gt_delt, gs_delt.

*-- Save data
  MODIFY zc103fit0003 FROM TABLE lt_save.
  IF sy-subrc EQ 0.
    MESSAGE '데이터가 저장되었습니다.' TYPE 'S'.
    COMMIT WORK.
  ELSE.
    ROLLBACK WORK.
  ENDIF.

  PERFORM get_data.
  PERFORM set_screen.
  PERFORM refresh_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form confirm
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LV_ANSWER
*&---------------------------------------------------------------------*
FORM confirm  CHANGING pv_answer.

  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = '저장'
    IMPORTING
      ev_answer = pv_answer.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_screen .

  DATA : lv_tabix TYPE sy-tabix,
         ls_style TYPE lvc_s_styl.

  LOOP AT gt_body INTO gs_body.

    lv_tabix = sy-tabix.

    CLEAR : ls_style, gs_body-cell_tab.

    ls_style-fieldname = 'TXT50'.
    ls_style-style = cl_gui_alv_grid=>mc_style_enabled.
    INSERT ls_style INTO TABLE gs_body-cell_tab.

    ls_style-fieldname = 'XINTB'.
    ls_style-style = cl_gui_alv_grid=>mc_style_enabled.
    INSERT ls_style INTO TABLE gs_body-cell_tab.

    ls_style-fieldname = 'XLOEV'.
    ls_style-style = cl_gui_alv_grid=>mc_style_enabled.
    INSERT ls_style INTO TABLE gs_body-cell_tab.

    ls_style-fieldname = 'KONTS'.
    ls_style-style = cl_gui_alv_grid=>mc_style_enabled.
    INSERT ls_style INTO TABLE gs_body-cell_tab.


    ls_style-fieldname = 'WAERS'.
    ls_style-style = cl_gui_alv_grid=>mc_style_enabled.
    INSERT ls_style INTO TABLE gs_body-cell_tab.


    MODIFY gt_body FROM gs_body INDEX lv_tabix TRANSPORTING cell_tab.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_data .

  SELECT saknr saknr_gr_cd bukrs txt50 xintb xloev konts waers erdat erzet ernam aedat aezet aenam
    INTO CORRESPONDING FIELDS OF TABLE gt_body
    FROM zc103fit0003
    WHERE bukrs = '0001'
    ORDER BY saknr.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_description
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_description .

  CASE gv_saknr_gr_cd.
    WHEN '00'.
      gv_description = '100000~199999까지 입력 가능합니다.'.
    WHEN '01'.
      gv_description = '200000~299999까지 입력 가능합니다.'.
    WHEN '02'.
      gv_description = '300000~399999까지 입력 가능합니다.'.
    WHEN '03'.
      gv_description = '400000~499999까지 입력 가능합니다.'.
    WHEN '04'.
      gv_description = '500000~599999까지 입력 가능합니다.'.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form search
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM search .

  IF gv_saknr IS INITIAL.

    CASE gv_saknr_gr_cd.
      WHEN '00'.
        SELECT saknr saknr_gr_cd bukrs txt50 xintb xloev konts waers erdat erzet ernam aedat aezet aenam
        INTO CORRESPONDING FIELDS OF TABLE gt_body
        FROM zc103fit0003
        WHERE saknr >= '100000' AND saknr < '200000'
        AND bukrs = '0001'
        ORDER BY saknr.

      WHEN '01'.
        SELECT saknr saknr_gr_cd bukrs txt50 xintb xloev konts waers erdat erzet ernam aedat aezet aenam
        INTO CORRESPONDING FIELDS OF TABLE gt_body
        FROM zc103fit0003
        WHERE saknr >= '200000' AND saknr < '300000'
        AND bukrs = '0001'
        ORDER BY saknr.

      WHEN '02'.
        SELECT saknr saknr_gr_cd bukrs txt50 xintb xloev konts waers erdat erzet ernam aedat aezet aenam
        INTO CORRESPONDING FIELDS OF TABLE gt_body
        FROM zc103fit0003
        WHERE saknr >= '300000' AND saknr < '400000'
        AND bukrs = '0001'
        ORDER BY saknr.

      WHEN '03'.
        SELECT saknr saknr_gr_cd bukrs txt50 xintb xloev konts waers erdat erzet ernam aedat aezet aenam
        INTO CORRESPONDING FIELDS OF TABLE gt_body
        FROM zc103fit0003
        WHERE saknr >= '400000' AND saknr < '500000'
        AND bukrs = '0001'
        ORDER BY saknr.

      WHEN '04'.
        SELECT saknr saknr_gr_cd bukrs txt50 xintb xloev konts waers erdat erzet ernam aedat aezet aenam
        INTO CORRESPONDING FIELDS OF TABLE gt_body
        FROM zc103fit0003
        WHERE saknr >= '500000' AND saknr < '600000'
        AND bukrs = '0001'
        ORDER BY saknr.

      WHEN OTHERS.
    ENDCASE.

  ELSE.
    " 그룹코드별 계정번호 범위 체크
    DATA(lv_valid) = abap_true.

    CASE gv_saknr_gr_cd.
      WHEN '00'.
        IF gv_saknr < '100000' OR gv_saknr > '199999'.
          lv_valid = abap_false.
        ENDIF.
      WHEN '01'.
        IF gv_saknr < '200000' OR gv_saknr > '299999'.
          lv_valid = abap_false.
        ENDIF.
      WHEN '02'.
        IF gv_saknr < '300000' OR gv_saknr > '399999'.
          lv_valid = abap_false.
        ENDIF.
      WHEN '03'.
        IF gv_saknr < '400000' OR gv_saknr > '499999'.
          lv_valid = abap_false.
        ENDIF.
      WHEN '04'.
        IF gv_saknr < '500000' OR gv_saknr > '599999'.
          lv_valid = abap_false.
        ENDIF.
    ENDCASE.

    IF lv_valid = abap_false.
      MESSAGE s015 DISPLAY LIKE 'E'.
      RETURN.
    ELSE.

      SELECT saknr saknr_gr_cd bukrs txt50 xintb xloev konts waers erdat erzet ernam aedat aezet aenam
      INTO CORRESPONDING FIELDS OF TABLE gt_body
      FROM zc103fit0003
      WHERE saknr = gv_saknr
      AND bukrs = '0001'.
      PERFORM refresh_table.
    ENDIF.
  ENDIF.

  PERFORM set_screen.
  PERFORM refresh_table.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form upload_logic
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM upload_logic .

  " 엑셀 데이터 타입 정의
  TYPES: truxs_t_text_data(4096) TYPE c OCCURS 0.

  " 파일 다이얼로그용 변수
  DATA: lt_files  TYPE filetable,
        ls_files  LIKE LINE OF lt_files,
        lv_filter TYPE string,
        lv_path   TYPE rlgrap-filename,
        lv_rc     TYPE i.

  " 엑셀 읽기용 변수
  DATA: lt_excel TYPE TABLE OF alsmex_tabline WITH HEADER LINE,
        lv_index TYPE sy-tabix.

  " 필드 매핑용 필드심볼
  FIELD-SYMBOLS: <field>.

  " 초기화
  CLEAR: gt_excel, gs_excel, lv_index.

*-----------------------------------------------------------------------
* 1. 파일 다이얼로그
*-----------------------------------------------------------------------
  CONCATENATE cl_gui_frontend_services=>filetype_excel
              'Excel 문서 (*.XLS)|*.XLS|'
              INTO lv_filter.

  CALL METHOD cl_gui_frontend_services=>file_open_dialog
    EXPORTING
      window_title            = '파일 열기'
      file_filter             = lv_filter
    CHANGING
      file_table              = lt_files
      rc                      = lv_rc
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5.

  IF sy-subrc <> 0 OR lt_files IS INITIAL.
    MESSAGE '파일을 선택하지 않았습니다.' TYPE 'S' DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  ls_files = lt_files[ 1 ].
  lv_path  = ls_files-filename.

*-----------------------------------------------------------------------
* 2. 엑셀 읽기
*-----------------------------------------------------------------------
  CALL FUNCTION 'ALSM_EXCEL_TO_INTERNAL_TABLE'
    EXPORTING
      filename                = lv_path
      i_begin_col             = 3
      i_begin_row             = 5
      i_end_col               = 100
      i_end_row               = 50000
    TABLES
      intern                  = lt_excel
    EXCEPTIONS
      inconsistent_parameters = 1
      upload_ole              = 2
      OTHERS                  = 3.

  IF sy-subrc <> 0 OR lt_excel[] IS INITIAL.
    MESSAGE '엑셀 업로드 중 오류가 발생했습니다.' TYPE 'E'.
    RETURN.
  ENDIF.

  SORT lt_excel BY row col.

*-----------------------------------------------------------------------
* 3. 엑셀 → 내부 구조 매핑
*-----------------------------------------------------------------------
  LOOP AT lt_excel.

    lv_index = lt_excel-col.
    IF lv_index = '0002'.

      " SAKNR 유효성 검증 (10자리 & 5번째 문자 1~5 확인)
      IF strlen( lt_excel-value ) <> 6 OR
         ( lt_excel-value(1) <> '1' AND
           lt_excel-value(1) <> '2' AND
           lt_excel-value(1) <> '3' AND
           lt_excel-value(1) <> '4' AND
           lt_excel-value(1) <> '5' ).
        MESSAGE 'G/L계정번호(SAKNR)를 확인해주세요.' TYPE 'S' DISPLAY LIKE 'E'.
        RETURN.
      ENDIF.
    ENDIF.
    ASSIGN COMPONENT lv_index OF STRUCTURE gs_excel TO <field>.
    IF sy-subrc = 0.
      <field> = lt_excel-value.
    ENDIF.

    AT END OF row.
      APPEND gs_excel TO gt_excel.
      CLEAR gs_excel.
    ENDAT.

  ENDLOOP.

*-----------------------------------------------------------------------
* 4. 결과 적용
*-----------------------------------------------------------------------
  LOOP AT gt_excel INTO gs_excel.

    CLEAR gs_body.
    MOVE-CORRESPONDING gs_excel TO gs_body.

    " 회사코드 검증
    IF gs_body-bukrs <> '0001'.
      MESSAGE '자사 회사코드만 입력 가능합니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.


    " 중복 여부 체크: PK 기준 (예: 회사코드 + 계정번호)
    READ TABLE gt_body WITH KEY bukrs = gs_body-bukrs
                                 saknr = gs_body-saknr
                          TRANSPORTING NO FIELDS.
    IF sy-subrc = 0.
      MESSAGE '중복된 PK가 존재합니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.


    " XINTB와 XLOEV 값 검증
    IF ( gs_body-xintb <> 'X' AND gs_body-xintb <> ' ' )
    OR ( gs_body-xloev <> 'X' AND gs_body-xloev <> ' ' ).
      MESSAGE '자동전표여부(XINTB), 삭제여부(XLOEV)는 X나 공백만 가능합니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    IF gs_body-waers <> 'KRW' AND gs_body-waers <> 'USD'.
      MESSAGE '통화코드(WAERS)는 KRW나 USD만 가능합니다.' TYPE 'S' DISPLAY LIKE 'E'.
      RETURN.
    ENDIF.

    gs_body-modi_yn = 'X'.

    APPEND gs_body TO gt_body.

  ENDLOOP.

  PERFORM refresh_table.

  MESSAGE |총 { lines( gt_excel ) }건의 데이터를 업로드했습니다.| TYPE 'S'.



ENDFORM.
*&---------------------------------------------------------------------*
*& Form download_logic
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM download_logic .

  DATA : lt_roid   TYPE lvc_t_roid,
         ls_roid   TYPE lvc_s_roid,
         lv_line   TYPE i,
         lv_answer.

*-- 선택행 정보를 가져온다.
  CALL METHOD go_alv_grid->get_selected_rows
    IMPORTING
      et_row_no = lt_roid.

*-- 선택행 없으면 오류메시지
  IF lt_roid IS INITIAL.
    MESSAGE s001 WITH TEXT-e01 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

*-- Dialog box
  CALL FUNCTION 'ZC1F030001'
    EXPORTING
      iv_action = '엑셀로 출력'
    IMPORTING
      ev_answer = lv_answer.

  IF lv_answer = '2'.  " No를 선택하면 종료
    RETURN.
  ENDIF.

*-- 선택행의 전표정보 발췌
  CLEAR: gt_selected_body, gs_selected_body.

  LOOP AT lt_roid INTO ls_roid.
    READ TABLE gt_body INDEX ls_roid-row_id INTO gs_selected_body.
    IF sy-subrc = 0.
      APPEND gs_selected_body TO gt_selected_body.
    ENDIF.
  ENDLOOP.

  IF objfile IS INITIAL.
    TRY .
        CREATE OBJECT objfile.
    ENDTRY.
  ENDIF.

*-- Open file save window -------------------------------------------*
  IF pfolder IS NOT INITIAL.
    initialfolder = pfolder.
  ELSE.
    objfile->get_temp_directory( CHANGING temp_dir = initialfolder
                                 EXCEPTIONS cntl_error = 1
                                           error_no_gui = 2
                                           not_supported_by_gui = 3 ).
  ENDIF.

  objfile->directory_browse( EXPORTING initial_folder = initialfolder
                             CHANGING selected_folder = pickedfolder
                             EXCEPTIONS cntl_error = 1
                                        error_no_gui = 2
                                        not_supported_by_gui = 3 ).

  IF sy-subrc = 0.
    pfolder = pickedfolder.
  ELSE.
    MESSAGE 'An error has occured picking a folder' TYPE 'I' DISPLAY LIKE 'W'.
    EXIT.
  ENDIF.
*--------------------------------------------------------------------*
  IF pfolder IS INITIAL.
    EXIT.
  ENDIF.

  CALL METHOD objfile->free.
  FREE objfile.

  PERFORM download_excel.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form download_excel
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM download_excel .

  DATA : lv_rc TYPE i.
  DATA: lv_date TYPE c LENGTH 8,
        lv_time TYPE c LENGTH 4,
        lv_name TYPE rlgrap-filename.

*-- 파일명 만들기용 기준 데이터
  READ TABLE gt_selected_body INTO gs_selected_body INDEX 1.
  IF sy-subrc <> 0.
    MESSAGE '선택된 데이터가 없습니다.' TYPE 'E'.
    EXIT.
  ENDIF.

*-- File name
  CLEAR gv_temp_filename.
  lv_date = sy-datum.         " 오늘 날짜 (예: 20250417)
  lv_time = sy-uzeit(4).    " 시분만 (예: 1352)

  CONCATENATE 'GL계정' '_' lv_date '_' lv_time '.XLS'
    INTO lv_name.

  CONCATENATE pfolder '\' lv_name INTO gv_temp_filename.

  gv_form = 'ZGL_ACCOUNT'.

  PERFORM download_template   USING gv_form gv_temp_filename.
  PERFORM open_excel_template USING gv_form.
  PERFORM fill_excel_line.

*-- 기본적으로 Sheet 1을 보여주도록 셋팅
  CALL METHOD OF excel 'SHEETS' = sheet EXPORTING #1 = 1.
  CALL METHOD OF sheet 'SELECT' NO FLUSH.

*-- 모두 출력 후 커서 맨 위로 이동
  CALL METHOD OF excel 'Cells' = cell
    EXPORTING
      #1 = 1
      #2 = 1.
  CALL METHOD OF cell 'Select'.

  SET PROPERTY OF excel 'VISIBLE' = 1 .

  MESSAGE s001 WITH TEXT-s01.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form download_template
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GV_FORM
*&      --> GV_TEMP_FILENAME
*&---------------------------------------------------------------------*
FORM download_template  USING p_zform p_filename.

  DATA : wwwdata_item LIKE wwwdatatab,
         rc           TYPE i.

  gv_file = p_filename.

  CALL FUNCTION 'WS_FILE_DELETE'
    EXPORTING
      file   = gv_file
    IMPORTING
      return = rc.

  IF rc = 0 OR rc = 1.

  ELSE.
    MESSAGE e001  WITH '임시파일 초기화 실패.'
                       '이전에 Excel에서 자료를 OPEN하였는지 확인.'.
  ENDIF.

  SELECT SINGLE * FROM wwwdata
    INTO CORRESPONDING FIELDS OF wwwdata_item
   WHERE objid = p_zform.

  CALL FUNCTION 'DOWNLOAD_WEB_OBJECT'
    EXPORTING
      key         = wwwdata_item
      destination = gv_file.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_excel_line
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM fill_excel_line .

  DATA : lv_line TYPE i,
         lv_cnt  TYPE i.

  CLEAR: gs_selected_body.

  lv_line   = 5.
  lv_cnt    = 1.

  LOOP AT gt_selected_body INTO gs_selected_body.

    PERFORM fill_cells USING lv_line  2 lv_cnt.
    PERFORM fill_cells USING lv_line  3 gs_selected_body-bukrs.
    PERFORM fill_cells USING lv_line  4 gs_selected_body-saknr.
    PERFORM fill_cells USING lv_line  5 gs_selected_body-txt50.
    PERFORM fill_cells USING lv_line  6 gs_selected_body-xintb.
    PERFORM fill_cells USING lv_line  7 gs_selected_body-xloev.
    PERFORM fill_cells USING lv_line  8 gs_selected_body-waers.

    lv_line += 1.
    lv_cnt += 1.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form open_excel_template
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GV_FORM
*&---------------------------------------------------------------------*
FORM open_excel_template  USING p_zform.

  IF excel IS INITIAL.
    CREATE OBJECT excel 'EXCEL.APPLICATION'.
  ENDIF.

  IF sy-subrc NE 0.
    MESSAGE i001 WITH sy-msgli.
  ENDIF.

  CALL METHOD OF excel 'WORKBOOKS' = workbook.
  SET PROPERTY OF excel 'VISIBLE' = 0 .

  CALL METHOD OF workbook 'OPEN' EXPORTING #1 = gv_file.

*-- Sheet에대한 설정을 할때 사용된다.






  GET PROPERTY OF : workbook    'Application' = application,
                    application 'ActiveSheet' = activesheet.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_cells
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_10
*&      --> P_3
*&      --> GS_BODY_BKTXT
*&---------------------------------------------------------------------*
FORM fill_cells  USING i j val.

  CALL METHOD OF excel 'CELLS' = cell
    EXPORTING
      #1 = i  " 행
      #2 = j. " 열

  SET PROPERTY OF cell 'VALUE' = val.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_chart
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_chart .

  CLEAR : go_ixml,
          go_ixml_sf,
          go_ixml_docu,
          go_ixml_ostream,
          go_ixml_encoding,
          go_chartdata,
          go_categories,
          go_category,
          go_series,
          go_point,
          go_value.

  CLEAR : gv_xstring.

  go_ixml = cl_ixml=>create(  ).
  go_ixml_sf = go_ixml->create_stream_factory( ).

  go_ixml_docu = go_ixml->create_document( ).

  go_ixml_encoding = go_ixml->create_encoding(
                       byte_order    = if_ixml_encoding=>co_little_endian
                       character_set = 'utf-8' ).

  go_ixml_docu->set_encoding( encoding = go_ixml_encoding ).

*-- Now build a DOM, representing an XML document with chart data
  go_chartdata = go_ixml_docu->create_simple_element(
                                        name   = 'ChartData'
                                        parent = go_ixml_docu ).

*-- Categories (parent)
  go_categories = go_ixml_docu->create_simple_element(
                                        name   = 'Categories'
                                        parent = go_chartdata ).

  PERFORM set_category_value.
  PERFORM set_chart_value.
  PERFORM design_mode.

  go_chart->set_data( xdata = gv_xstring ).
  go_chart->render( ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_category_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_category_value .

  DATA : lv_value TYPE string.

*-- Categories (children)
  go_category = go_ixml_docu->create_simple_element(
                                        name   = 'Category'
                                        parent = go_categories ).
  go_category->if_ixml_node~set_value( 'G/L계정' ). " SeatsMax

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_chart_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_chart_value .

  TYPES: BEGIN OF ty_code,
           code TYPE string,
           text TYPE string,
         END OF ty_code.

  DATA: lt_codes TYPE STANDARD TABLE OF ty_code WITH EMPTY KEY,
        ls_code  TYPE ty_code,
        lv_count TYPE i,
        lv_value TYPE string.

*-- 고정된 코드값과 의미
  lt_codes = VALUE #( ( code = '00' text = '자산' )
                      ( code = '01' text = '부채' )
                      ( code = '02' text = '자본' )
                      ( code = '03' text = '수익' )
                      ( code = '04' text = '비용' ) ).

*-- lt_codes를 루프하면서
  LOOP AT lt_codes INTO ls_code.

*-- 해당 코드의 개수 세기
    CLEAR lv_count.
    LOOP AT gt_body INTO gs_body WHERE saknr_gr_cd = ls_code-code.
      lv_count = lv_count + 1.
    ENDLOOP.

*-- Series 생성
    go_series = go_ixml_docu->create_simple_element(
                                                      name = 'Series'
                                                      parent = go_chartdata ).
    go_series->set_attribute( name = 'label'
                              value = ls_code-text ).

*-- Point 생성 (항상 '계정수' 하나만)
    go_point = go_ixml_docu->create_simple_element(
                                                    name = 'Point'
                                                    parent = go_series ).
    go_point->set_attribute( name = 'label'
                             value = '계정수' ).

    go_value = go_ixml_docu->create_simple_element(
                                                    name = 'Value'
                                                    parent = go_point ).
    go_value->if_ixml_node~set_value( |{ lv_count }| ).

  ENDLOOP.

*-- Create stream
  go_ixml_ostream = go_ixml_sf->create_ostream_xstring( gv_xstring ).
  go_ixml_docu->render( go_ixml_ostream ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form design_mode
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM design_mode .

**********************************************************************
* Chart type : Only Columns and Lines
**********************************************************************
  DATA: l_win_chart   TYPE REF TO cl_gui_chart_engine_win,
        g_design_mode.

  CATCH SYSTEM-EXCEPTIONS move_cast_error = 1.
    l_win_chart ?= go_chart->get_control( ).
  ENDCATCH.

  IF sy-subrc IS INITIAL.

    l_win_chart->set_design_mode( flag = g_design_mode event = 'X' ).
    l_win_chart->restrict_chart_types( charttypes = 'Columns' ).
*    l_win_chart->restrict_chart_types( charttypes = 'Lines' ).
    l_win_chart->restrict_property_events( events = 'ChartType' ).

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form count_account_by_category
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM count_account_by_category .

  CLEAR: gv_cnt_asset, gv_cnt_liability, gv_cnt_capital, gv_cnt_expense, gv_cnt_revenue.

  LOOP AT gt_body INTO gs_body.
    CASE gs_body-saknr_gr_cd.
      WHEN '00'. " 자산
        gv_cnt_asset = gv_cnt_asset + 1.
      WHEN '01'. " 부채
        gv_cnt_liability = gv_cnt_liability + 1.
      WHEN '02'. " 자본
        gv_cnt_capital = gv_cnt_capital + 1.
      WHEN '04'. " 비용
        gv_cnt_expense = gv_cnt_expense + 1.
      WHEN '03'. " 수익
        gv_cnt_revenue = gv_cnt_revenue + 1.
    ENDCASE.
  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form all_logic
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM all_logic .

  PERFORM get_data.
  PERFORM set_screen.
  PERFORM refresh_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form form_download_logic
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM form_download_logic .

  DATA : wwwdata_item TYPE wwwdatatab,
         lv_filename  TYPE rlgrap-filename,
         lv_rc        TYPE i,
         lv_answer    TYPE c.

*-- 사용자에게 폴더 선택 요청
  IF objfile IS INITIAL.
    CREATE OBJECT objfile.
  ENDIF.

  objfile->get_temp_directory( CHANGING temp_dir = initialfolder ).
  objfile->directory_browse( EXPORTING initial_folder = initialfolder
                             CHANGING selected_folder = pickedfolder ).
  IF pickedfolder IS INITIAL.
    MESSAGE '폴더 선택이 취소되었습니다.' TYPE 'I'.
    RETURN.
  ENDIF.

*-- 파일명 정의
  CONCATENATE pickedfolder '\GL계정_엑셀업로드양식.XLS' INTO lv_filename.

*-- 기존 파일 삭제 (있다면)
  CALL FUNCTION 'WS_FILE_DELETE'
    EXPORTING
      file   = lv_filename
    IMPORTING
      return = lv_rc.

*-- wwwdata에서 템플릿 가져오기
  SELECT SINGLE * FROM wwwdata
    INTO CORRESPONDING FIELDS OF wwwdata_item
    WHERE objid = 'ZGL_ACCOUNT'.  " 등록된 템플릿 이름

  IF sy-subrc <> 0.
    MESSAGE '엑셀 템플릿이 시스템에 존재하지 않습니다.' TYPE 'E'.
    RETURN.
  ENDIF.

*-- 템플릿 다운로드
  CALL FUNCTION 'DOWNLOAD_WEB_OBJECT'
    EXPORTING
      key         = wwwdata_item
      destination = lv_filename.

  MESSAGE '엑셀 템플릿이 다운로드되었습니다.' TYPE 'S'.

ENDFORM.
